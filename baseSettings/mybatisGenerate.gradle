

configurations {
    mybatisGenerator
}

dependencies{
    compile 'org.mybatis:mybatis:3.4.1'
    compile 'org.mybatis:mybatis-spring:1.3.0'
    compile 'tk.mybatis:mapper:3.3.8'
    compile 'com.github.pagehelper:pagehelper:4.1.6'
    compile 'com.microsoft.sqlserver:sqljdbc4:4.0'
    mybatisGenerator  'org.mybatis.generator:mybatis-generator-core:1.3.2'
    mybatisGenerator  'com.microsoft.sqlserver:sqljdbc4:4.0'
    mybatisGenerator  'tk.mybatis:mapper:3.3.8'
    //mybatisGenerator group: 'com.hynnet', name: 'sqljdbc4-chs', version: '4.0.2206.100'
}

//获取项目jdbc配置
def getDbProperties = {
    def properties = new Properties()

    file("$projectDir/src/main/resources/jdbc.properties").withInputStream { inputStream ->
        properties.load(inputStream)
    }
    properties;
}

//mybatis自动生成任务
task mybatisGenerate << {

    def properties = getDbProperties()
    ant.properties['targetProject'] = projectDir.path
    ant.properties['driver'] = properties.getProperty("jdbc.driver")
    ant.properties['url'] = properties.getProperty("jdbc.url")
    ant.properties['user'] = properties.getProperty("jdbc.username")
    ant.properties['password'] = properties.getProperty("jdbc.password")
    ant.properties['src_main_java'] = sourceSets.main.java.srcDirs[0].path
    ant.properties['src_main_resources'] = sourceSets.main.resources.srcDirs[0].path
    ant.properties['modelPackage'] = this.modelPackage
    ant.properties['mapperPackage'] = this.mapperPackage
    ant.properties['sqlMapperPackage'] = this.sqlMapperPackage
    ant.taskdef(
            name: 'mbgenerator',
            classname: 'org.mybatis.generator.ant.GeneratorAntTask',
            classpath: configurations.mybatisGenerator.asPath
    )
    ant.mbgenerator(overwrite: true,configfile: "$projectDir/config/mybatisGenerator.xml", verbose: true) {
        propertyset {
            propertyref(name: 'targetProject')
            propertyref(name: 'user')
            propertyref(name: 'driver')
            propertyref(name: 'url')
            propertyref(name: 'password')
            propertyref(name: 'src_main_java')
            propertyref(name: 'src_main_resources')
            propertyref(name: 'modelPackage')
            propertyref(name: 'mapperPackage')
            propertyref(name: 'sqlMapperPackage')
        }
    }
}


